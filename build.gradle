buildscript {
    repositories { 
        mavenCentral()
    }
}

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'net.researchgate.release' version '3.0.2'
  id 'com.diffplug.spotless' version '6.11.0'
  id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
}

configurations {
  provided
}

sourceSets {
  main {
    compileClasspath += configurations.provided
    runtimeClasspath += configurations.provided
  }
  test {
    compileClasspath += configurations.provided
    runtimeClasspath += configurations.provided
  }
}

spotless {
  java {
    target 'src/**/*.java'
    importOrder()
    removeUnusedImports()
    googleJavaFormat()
  }
}

java {
  sourceCompatibility = JavaVersion.toVersion("11")
  targetCompatibility = JavaVersion.toVersion("11")
  withJavadocJar()
  withSourcesJar()
}

test {
  useJUnitPlatform()
}

repositories {
  mavenCentral()
}

dependencies {

  provided group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.1'

  implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.1'
  implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.2'
  implementation group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.2'
  implementation group: 'io.jsonwebtoken', name: 'jjwt-gson', version: '0.11.2'

  provided platform('io.projectreactor:reactor-bom:2020.0.11')
  provided 'io.projectreactor.netty:reactor-netty-core'
  provided 'io.projectreactor.netty:reactor-netty-http'
  implementation group: 'io.projectreactor.addons', name: 'reactor-extra', version: '3.4.5'
  implementation 'com.github.ben-manes.caffeine:caffeine:3.0.4'

  testCompileOnly group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
  testCompileOnly group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.2.4'
  testCompileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.6.0'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.0'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.15.0'
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.0'

}

nexusPublishing {
  repositories {
    sonatype()
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from(components.java)
        pom {
          name = 'authkit-java'
          description = 'OpenID and JWT client for Java Applications'
          url = 'https://authkit.com'
          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          developers {
            developer {
              id = 'btomasini'
              name = 'Ben Tomasini'
              email = 'btomasini@authkit.com'
            }
          }
          scm {
            connection = 'scm:git:git://github.com/authkitcom/authkit-java.git'
            developerConnection = 'scm:git:git://github.com/authkitcom/authkit-java.git'
            url = 'https://github.com/authkitcom/authkit-java'
          }
        }
    }
  }
}

release {
    afterReleaseBuild.dependsOn publish
    git {
      requireBranch.set('')
    }
}

signing {
  sign publishing.publications.mavenJava
}



